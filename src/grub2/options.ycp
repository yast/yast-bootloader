/**
 * File:
 *      include/bootloader/routines/grub2/options.ycp
 *
 * Module:
 *      Bootloader installation and configuration
 *
 * Summary:
 *      Grub2 widgets for bootloader options.
 *
 * Authors:
 *      Josef Reidinger<jreidinger@suse.cz>
 *
 * $Id: $
 *
 */

{
textdomain "bootloader";

import "Label";
import "Initrd";
import "System::Bootloader_API";

include "bootloader/routines/common_options.ycp";
include "bootloader/grub/helps.ycp";
include "bootloader/grub2/helps.ycp";

list<map> vga_modes = [];

/**
 * Init function of widget
 * @param widget any id of the widget
 */
void VgaModeInit (string widget) {

    if ( size(vga_modes) == 0) {
        vga_modes = Initrd::VgaModes ();
    }

    list items = maplist (map m, vga_modes, {
	return `item (`id (sformat ("%1",  tohexstring(m["mode"]:0))),
	    // combo box item
	    // %1 is X resolution (width) in pixels
	    // %2 is Y resolution (height) in pixels
	    // %3 is color depth (usually one of 8, 16, 24, 32)
	    // %4 is the VGA mode ID (hexadecimal number)
	    sformat (_("%1x%2, %3 bits (mode %4)"),
		m["width"]:0, m["height"]:0, m["color"]:0,  tohexstring(m["mode"]:0)));
    });
    items = prepend(items,`item(`id("ask"), _("Ask for resolution during boot.")));
    items = prepend(items,`item(`id("extended"), _("Standard 8-pixel font mode.")));
    // item of a combo box
    items = prepend (items, `item (`id ("normal"), _("Text Mode")));
    items = prepend (items, `item (`id (""), _("Unspecified")));
    UI::ChangeWidget (`id (widget), `Items, items);
    InitGlobalStr (widget);
}

void DefaultEntryInit (string widget) {
    list items = [];

    foreach (map<string,any> s, BootCommon::sections, {
        items = add (items, `item(`id(s["menuentry"]:""), s["menuentry"]:""));
    });

    UI::ChangeWidget (`id (widget), `Items, items);
    InitGlobalStr (widget);
    return;
}

/**
 * Init function for console
 * @param string widget
 */
void ConsoleInit(string widget){
    boolean enable = BootCommon::globals["terminal"]:"" == "serial";
    UI::ChangeWidget(`id(`console_frame),`Value, enable);
    string args = BootCommon::globals["serial"]:"";
    UI::ChangeWidget(`id(`console_args),`Value, args);

    enable = BootCommon::globals["terminal"]:"" == "gfxterm";
    UI::ChangeWidget(`id(`gfxterm_frame),`Value, enable);

    if ( size(vga_modes) == 0) {
        vga_modes = Initrd::VgaModes ();
    }

    list<map> vga_modes_sort = sort (map a, map b, vga_modes, {
        if (a["width"]:0 == b["width"]:0)
            return (a["height"]:0 > b["height"]:0);
        return (a["width"]:0 > b["width"]:0);
    });

    integer width = 0;
    integer height = 0;
    vga_modes_sort = filter ( map m , vga_modes_sort, ``{
        boolean ret = false;
        if (width != m["width"]:0 || height != m["height"]:0) {
            ret = true;
        }
        width = m["width"]:0;
        height = m["height"]:0;
        return ret;
    });

    list items = maplist (map m, vga_modes_sort, ``{
        string mode = sformat ("%1x%2", m["width"]:0, m["height"]:0);
	return `item (`id (mode), mode);
    });


    UI::ChangeWidget(`id(`gfxmode), `Items, items);
    string mode = BootCommon::globals["gfxmode"]:"";

    /* No mode pecified, use it */
    if (mode != "")
        UI::ChangeWidget(`id(`gfxmode), `Value, mode);

    UI::ChangeWidget(`id(`gfxbackground), `Value, BootCommon::globals["gfxbackground"]:"");
}

/**
 * Store function of a console
 * @param widget any widget key
 * @param event map event description of event that occured
 */
void ConsoleStore(string widget, map event){
    boolean use_serial = (boolean) UI::QueryWidget(`id(`console_frame),`Value);
    boolean use_gfxterm = (boolean) UI::QueryWidget(`id(`gfxterm_frame),`Value);

    if (use_gfxterm && use_serial) {
        use_gfxterm = false;
    } else if (!use_gfxterm && !use_serial) {
        BootCommon::globals["terminal"] = "console";
    }

    if (use_serial)
    {
        BootCommon::globals["terminal"] = "serial";
        string console_value = (string) UI::QueryWidget(`id(`console_args),`Value);
        if (console_value != "")
	    BootCommon::globals["serial"] = console_value;
    } else {
        if (haskey(BootCommon::globals, "serial"))
	    BootCommon::globals = remove(BootCommon::globals, "serial");
    }

    if (use_gfxterm)
    {
        BootCommon::globals["terminal"] = "gfxterm";
    }

    string mode = (string)UI::QueryWidget(`id(`gfxmode),`Value);
    if (mode != "")
        BootCommon::globals["gfxmode"] = mode;

    string background = (string)UI::QueryWidget(`id(`gfxbackground), `Value);
    BootCommon::globals["gfxbackground"] = background;

    // FATE: #110038: Serial console
    // add or remove console key with value for sections
    BootCommon::HandleConsole2();
}

symbol ConsoleHandle(string widget, map event){

  string backgrounds_dir = "/boot/grub2/backgrounds";

  if (SCR::Read (.target.size, backgrounds_dir) == -1) {
      backgrounds_dir = "/boot/grub2";
  }

  string file = UI::AskForExistingFile(backgrounds_dir, "*.png *.jpg",
                  _("Choose new graphical menu file"));

  if (file != nil){
      UI::ChangeWidget(`id(`gfxbackground),`Value, file);
  }

  return nil;
}

map<string,map<string,any> > Grub2Options(){
  map<string,map<string,any> >  grub2_specific = $[
    "activate" : CommonCheckboxWidget(grub_descriptions["activate"]:"activate",
                                      grub_help_messages["activate"]:""),
    "generic_mbr" : CommonCheckboxWidget(grub_descriptions["generic_mbr"]:"generic mbr",
                                         grub_help_messages["generic_mbr"]:""),
    "hiddenmenu" : CommonCheckboxWidget(grub_descriptions["hiddenmenu"]:"hidden menu",
                                        grub_help_messages["hiddenmenu"]:""),
    "append" : CommonInputFieldWidget(grub2_descriptions["append"]:"append",
                                        grub2_help_messages["append"]:""),
    "vgamode" : $[
        "widget" : `combobox,
        "label" : grub2_descriptions["vgamode"]:"vgamode",
        "opt" : [ `editable, `hstretch ],
        "init" : VgaModeInit,
        "store" : StoreGlobalStr,
        "help" : grub2_help_messages["vgamode"]:"",
    ],

    "default" : $[
        "widget" : `combobox,
        "label" : grub_descriptions["default"]:"default",
        "opt" : [ `editable, `hstretch ],
        "init" : DefaultEntryInit,
        "store" : StoreGlobalStr,
        "help" : grub_help_messages["default"]:"",
    ],

    "console" : $[
        "widget" : `custom,
        "custom_widget" : `VBox(
            `CheckBoxFrame(`id(`gfxterm_frame),_("Use &graphical console"),true,
            `HBox(
            `HSpacing(2),
            `ComboBox(`id(`gfxmode),`opt(`editable, `hstretch), _("&Console resolution"), [""]),
            `HBox(`Left(`InputField(`id(`gfxbackground),`opt(`hstretch), _("&Console background"))),
            `VBox(
            `Left(`Label("")),
            `Left(`PushButton(`id(`browsegfx),`opt(`notify), Label::BrowseButton())))),
            `HStretch())),
            `CheckBoxFrame(`id(`console_frame),_("Use &serial console"),true,
            `HBox(
            `HSpacing(2),
            `InputField(`id(`console_args),`opt(`hstretch), _("&Console arguments")),
            `HStretch()))
        ),
        "init" : ConsoleInit,
        "store" : ConsoleStore,
        "handle" : ConsoleHandle,
        "handle_events" : [`browsegfx],
        "help" : grub_help_messages["serial"]:""
    ],

  ];

  return (map<string,map<string,any> >)union(grub2_specific,CommonOptions());
}

}

